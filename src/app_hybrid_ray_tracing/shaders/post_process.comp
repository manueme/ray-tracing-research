/*
 * Manuel Machado Copyright (C) 2021 This code is licensed under the MIT license (MIT)
 * (http://opensource.org/licenses/MIT)
 */

#version 450

#extension GL_GOOGLE_include_directive : enable

#define SCENE_SET 0 // override SCENE_SET for this particular case
#include "../../framework/shaders/exposure_functions.glsl"
#include "app_scene.glsl"

layout(local_size_x = 16, local_size_y = 16) in;

layout(set = 1, binding = 0) uniform sampler2D inputTexture;
layout(set = 2, binding = 0) readonly buffer _AutoExposure { float exposure; }
exposureSettings;
layout(set = 3, binding = 0, rgba8) uniform image2D outputTexture;

#define FXAA_REDUCE_MIN (1.0 / 128.0)
#define FXAA_REDUCE_MUL (1.0 / 8.0)
#define FXAA_SPAN_MAX 8.0

vec4 fxaa_process(vec2 resolution, vec2 uv)
{
    vec3 m = texture(inputTexture, uv).xyz;
    vec3 nw = textureOffset(inputTexture, uv, ivec2(-1.0, -1.0)).xyz;
    vec3 ne = textureOffset(inputTexture, uv, ivec2(1.0, -1.0)).xyz;
    vec3 sw = textureOffset(inputTexture, uv, ivec2(-1.0, 1.0)).xyz;
    vec3 se = textureOffset(inputTexture, uv, ivec2(1.0, 1.0)).xyz;

    vec3 luma = vec3(0.299, 0.587, 0.114);
    float lumaM = dot(m, luma);
    float lumaNW = dot(nw, luma);
    float lumaNE = dot(ne, luma);
    float lumaSW = dot(sw, luma);
    float lumaSE = dot(se, luma);

    float lumaMin = min(lumaM, min(min(lumaNW, lumaNE), min(lumaSW, lumaSE)));
    float lumaMax = max(lumaM, max(max(lumaNW, lumaNE), max(lumaSW, lumaSE)));
    vec2 dir
        = vec2(-((lumaNW + lumaNE) - (lumaSW + lumaSE)), ((lumaNW + lumaSW) - (lumaNE + lumaSE)));

    float dirReduce
        = max((lumaNW + lumaNE + lumaSW + lumaSE) * (0.25 * FXAA_REDUCE_MUL), FXAA_REDUCE_MIN);
    float rcpDirMin = 1.0 / (min(abs(dir.x), abs(dir.y)) + dirReduce);
    dir = min(vec2(FXAA_SPAN_MAX), max(vec2(-FXAA_SPAN_MAX), dir * rcpDirMin)) / resolution;

    vec3 rgbA = 0.5
        * (texture(inputTexture, uv + dir * (1.0 / 3.0 - 0.5)).xyz
            + texture(inputTexture, uv + dir * (2.0 / 3.0 - 0.5)).xyz);
    vec3 rgbB = rgbA * 0.5
        + 0.25
            * (texture(inputTexture, uv + dir * -0.5).xyz
                + texture(inputTexture, uv + dir * 0.5).xyz);
    float lumaB = dot(rgbB, luma);
    if (lumaB < lumaMin || lumaB > lumaMax) {
        return vec4(rgbA, 1.0);
    } else {
        return vec4(rgbB, 1.0);
    }
}

void main()
{
    vec2 res = textureSize(inputTexture, 0);
    vec2 q = gl_GlobalInvocationID.xy / res;
    if (q.x > 1 || q.y > 1) {
        return;
    }

    // Vignette fffect
    vec2 v = -1.0 + 2.0 * q;
    v.x *= res.x / res.y;
    float vignette = smoothstep(4.0, 0.6, length(v));
    // ###

    // Chromatic aberration effect with tone mapping
    vec2 centerToUv = q - vec2(0.5);
    vec3 aberr;
    aberr.x = fxaa_process(res, vec2((0.5 + centerToUv * 0.995))).x;
    aberr.y = fxaa_process(res, vec2((0.5 + centerToUv * 0.997))).y;
    aberr.z = fxaa_process(res, vec2((0.5 + centerToUv))).z;

    vec4 result = vec4(
        (vignette
            * linear_tone_mapping(aberr, exposureSettings.exposure + scene.manualExposureAdjust)),
        1.0);
    // ###

    imageStore(outputTexture, ivec2(gl_GlobalInvocationID.xy), result);
}